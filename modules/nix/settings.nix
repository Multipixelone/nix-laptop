{ lib, config, ... }:
{
  options.nix.settings = {
    keep-outputs = lib.mkOption { type = lib.types.bool; };
    warn-dirty = lib.mkOption { type = lib.types.bool; };
    abort-on-warn = lib.mkOption { type = lib.types.bool; };
    allow-import-from-derivation = lib.mkOption { type = lib.types.bool; };
    experimental-features = lib.mkOption {
      type = lib.types.listOf lib.types.singleLineStr;
      default = [ ];
    };
    extra-system-features = lib.mkOption {
      type = lib.types.listOf lib.types.singleLineStr;
      default = [ ];
    };
  };
  config = {
    nix.settings = {
      keep-outputs = true;
      warn-dirty = false;
      abort-on-warn = true;
      allow-import-from-derivation = false;
      experimental-features = [
        "nix-command"
        "flakes"
        "recursive-nix"
      ];
      extra-system-features = [ "recursive-nix" ];
    };
    flake.modules = {
      nixos.base.nix = {
        inherit (config.nix) settings;
      };

      homeManager.base.nix = {
        inherit (config.nix) settings;
      };

      nixOnDroid.base.nix.extraOptions =
        config.nix.settings
        |> lib.mapAttrsToList (name: value: "${name} = ${toString value}")
        |> lib.concatLines;
    };
  };
}
